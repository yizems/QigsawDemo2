apply plugin: 'com.android.application'
apply plugin: 'com.iqiyi.qigsaw.sample.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "cn.yizems.qigsawdemo"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        multiDexKeepProguard file('multidexkeep.pro')
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'arm64-v8a', 'x86'//for emulator
        }
    }
    signingConfigs {
        key {
            storeFile file('../key.jks')
            keyAlias 'demo'
            storePassword '123456'
            keyPassword '123456'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.key
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.key
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    dynamicFeatures = [':featuredemo']

}


def qigsawPath = file("qigsaw/")

qigsawSplit {

    /**
     * Whether repack base apk with 7z format.
     * default value is {@code false}
     */
    use7z = false

    /**
     * optional，default 'null'
     * if you want to update split version, oldApk must be set.
     */
    oldApk = "${qigsawPath}/app.apk"

    /**
     * optional，default 'null'
     * if you want to update splits, applyMapping must be set.
     */
    applyMapping = "${qigsawPath}/mapping.txt"

    /**
     * optional, default 'false'
     * Whether upload all split APKs to CDN, ignored 'onDemand' declared in split AndroidManifest file.
     */
    releaseSplitApk = true

    /**
     * Whether build multiple APKs for ABIs
     */
    multipleApkForABIs = true

    /**
     * optional, default '1.0.0'
     * when update splits, you need upgrade value of splitInfoVersion
     */
    splitInfoVersion = '1.0.0'

    /**
     * optional, default 'null'
     *
     * restrict split only work in processes which declared in split AndroidManifest file.
     */
    restrictWorkProcessesForSplits = ['java']

    /**
     * Split fragments which would be loaded by activities of base apk.(Not all fragments in split apk)
     */
    splitEntryFragments = ['com.iqiyi.qigsaw.sample.java.JavaSampleFragment']

    /**
     * Activities of base apk which would load split's fragments or resources.
     */
    baseContainerActivities = ["com.iqiyi.qigsaw.sample.MainActivity"]
}


splitUpload {

    /**
     * Whether upload all split apks to test env
     */
    useTestEnv = true

    /**
     * You can decide which split apks should be upload to test env.
     */
    testOnly = ["java"]
}


dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation "com.iqiyi.android.qigsaw:splitcore:${QIGSAW_VERSION}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}
